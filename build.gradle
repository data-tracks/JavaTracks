plugins {
    id 'java'
    // MIT
    id "io.freefair.lombok" version "8.10.2"
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = 1.16
    targetCompatibility = 1.16
}

// Ensure source and target compatibility
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17) // Or your desired Java version
    }
}

group = 'dev.datatracks'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Apache 2.0
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.18.1'
    // Apache 2.0
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.1'
    // Apache 2.0
    implementation group: 'com.google.flatbuffers', name: 'flatbuffers-java', version: '24.3.25'
    // Apache 2.0
    compileOnly 'org.jetbrains:annotations:26.0.2'


// Testing
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = configurations.annotationProcessor
}

tasks.register('copyProtocol', Copy) {
    dependsOn(":generateEffectiveLombokConfig")

    from "protocol/generated/java/protocol"
    into layout.buildDirectory.dir("generated/flatbuffers").get().asFile
}

compileJava.dependsOn copyProtocol

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/flatbuffers"  // Adds generated FlatBuffers classes to Java source
        }
    }
}

test {
    useJUnitPlatform()
}

jar {
    archiveBaseName = 'JavaTracks'
    archiveVersion = '0.0.1'
    archiveFileName = 'javatracks.jar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // Optional: Customize artifactId if different from project name
            // artifactId = 'my-awesome-library'

            pom {
                name = 'JavaTracks'
                description = 'Java client to interact with DataTracks.'
                url = 'https://github.com/data-tracks/JavaTracks' // Your project URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'datomo'
                        name = 'David Lengweiler'
                        email = 'dave.lengw@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/data-tracks/JavaTracks.git'
                    developerConnection = 'scm:git:ssh://github.com:data-tracks/JavaTracks.git'
                    url = 'https://github.com/data-tracks/JavaTracks'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? uri(snapshotsRepoUrl) : uri(releasesRepoUrl)
            credentials {
                // In GitHub Actions, these will be pulled from GitHub Secrets, exposed as env vars
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    // This part is crucial for CI/CD environments.
    // It tells Gradle to use an in-memory PGP key, which is imported via env variables.
    useGpgCmd() // This method uses the GPG command line tool

    // Provide the passphrase via project property or environment variable.
    // System.getenv is preferred for CI secrets.
    passphrase = System.getenv("GPG_PASSPHRASE")

    sign publishing.publications.mavenJava
}
